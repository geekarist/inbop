plugins {
    id 'com.github.ben-manes.versions' version '0.14.0'
}

apply plugin: 'com.android.application'
apply plugin: 'android-apt'
apply plugin: 'jacoco-everywhere'

//noinspection GroovyMissingReturnStatement
android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"
    defaultConfig {

        buildConfigField('String', 'PLACES_API_PATH', '"/geekarist/inbop-api/master/places.json"')

        applicationId "me.cpele.inbop"
        minSdkVersion 15
        targetSdkVersion 25
        versionName "0.1.0-b1"
        versionCode 6
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        resConfigs "en", "fr"

        multiDexEnabled true
    }

    buildTypes {

        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        debug {
            testCoverageEnabled = true
        }
    }

    productFlavors {

        // Standard development environment connected to remote dev backend
        development {
            buildConfigField('String', 'PLACES_API_BASE_URL', '"https://rawgit.com"')
            applicationId "me.cpele.inbop.dev"
        }

        // Specific development environment running in an emulator and connected to local backend
        offlineEmulation {
            buildConfigField('String', 'PLACES_API_BASE_URL', '"http://10.0.2.2:8000"')
            buildConfigField('String', 'PLACES_API_PATH', '"/places.json"')
            applicationId "me.cpele.inbop.offline"
        }

        // Production environment connected to remote prod backend
        production {
            buildConfigField('String', 'PLACES_API_BASE_URL', '"https://cdn.rawgit.com"')
            buildConfigField('String', 'PLACES_API_PATH', '"/geekarist/inbop-api/450a18a89157ef0ea765385d13e22e78c8867b93/places.json"')
            applicationId "me.cpele.inbop.prod"
        }
    }

    lintOptions {
        warning 'InvalidPackage'
        return true
    }
}

def supportVersion = '25.1.1'

dependencies {

    compile fileTree(dir: 'libs', include: ['*.jar'])

    compile "com.android.support:appcompat-v7:${supportVersion}"
    compile "com.android.support:recyclerview-v7:${supportVersion}"
    compile "com.android.support:cardview-v7:${supportVersion}"
    compile "com.android.support:appcompat-v7:${supportVersion}"
    compile "com.android.support:design:${supportVersion}"

    compile "com.android.support:multidex:1.0.1"

    //noinspection GradleDependency
    compile 'com.google.android.gms:play-services-maps:10.0.1'

    compile 'com.squareup.retrofit2:retrofit:2.1.0'
    compile 'com.squareup.retrofit2:converter-gson:2.1.0'

    compile 'com.github.bumptech.glide:glide:3.7.0'
    compile 'com.google.code.gson:gson:2.8.0'

    compile 'com.jakewharton:butterknife:8.5.1'
    apt 'com.jakewharton:butterknife-compiler:8.5.1'

    compile 'org.parceler:parceler-api:1.1.6'
    apt 'org.parceler:parceler:1.1.6'

    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:2.7.5'

    androidTestCompile('com.android.support.test.espresso:espresso-core:2.3-alpha', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
}

// Task to disable animations on the device using a shell command
task disableAnimations(type: Exec, dependsOn: 'installDevelopmentDebug') {
    commandLine "adb shell settings put global window_animation_scale 0".split(" ")
}

// Make any connected* task depend on disableAnimations
tasks.whenTaskAdded { task ->
    if (System.getenv()['CI'] && task.name.startsWith('connected')) {
        task.dependsOn disableAnimations
    }
}

// Skip tests for offlineEmulation variant
gradle.taskGraph.useFilter { task ->
    def taskName = task.name.toLowerCase()
    def skipTask = taskName.contains('test') && taskName.contains('offlineemulation')
    return !skipTask
}
